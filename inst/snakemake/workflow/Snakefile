report: "report/workflow.rst"

# Allow users to fix the underlying OS via singularity.
configfile: "config.yaml"

rule all:
    input: ["output/Obs.csv", "output/MR_all.csv"]

rule obs_analysis:
    input:
        "input/data_Obs.csv"
    output:
        "output/Obs.csv"
    conda:
        "envs/cvd1-inf1-hf.yaml"
    script:
        "scripts/Obs_analysis.R"

rule MR_analysis:
    input:
        data_MR="input/data_MR.csv",
        ldrho="input/cis200kb_ldrho/{protein}.ld",
        snplist="input/cis200kb_ldrho/{protein}.snplist"
    conda:
        "envs/cvd1-inf1-hf.yaml"
    output:
        "output/MR/{protein}.csv"
    script:
        "scripts/MR_analysis.R"

rule aggregate_MR:
    input:
        expand("output/MR/{protein}.csv", protein = config["target_protein_MR"])
    output:
        "output/MR_all.csv"
    shell:
        "awk 'NR == FNR {{print; next}} FNR > 1' {input} > {output}"
